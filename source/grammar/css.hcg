@SYMBOL || ^= $= *= <= >= . #

@IGNORE θnl θws

@IMPORT ../../node_modules/@candlefw/hydrocarbon/source/grammars/misc/default-productions.hcg as def 

##############################################################################################
##############################################################################################
#### CSS
##############################################################################################
##############################################################################################	

	<> CSS → STYLE_SHEET															↦c ^stylesheet

	<> STYLE_SHEET → import(*) ( STYLE_RULE │ AT_RULE )(*)							↦r { [$$sym1, $$sym2] }

	<> STYLE_RULE → COMPLEX_SELECTOR(+,) { declaration_list ;? } 					↦r {new env.fn.stylerule($$sym1, $$sym3)}

	<> GROUP_RULE_BODY → STYLE_RULE(*)

	<> AT_RULE → media ;?
		│ import ;?
		│ keyframes ;?
		│ supports ;?

##############################################################################################
##############################################################################################
#### @Rules
##############################################################################################
##############################################################################################	

# adapted from https://www.w3.org/TR/css-cascade-4/#at-import

	<> import →  \@ τimport θws(*) ( string │ url ) ( τsupports \( ( supports_condition │ declaration ) \) )? media_query(*,) 

# adapted from https://developer.mozilla.org/en-US/docs/Web/CSS/@keyframes#Formal_syntax

	<> keyframes → \@ τkeyframes keyframes_names { keyframes_blocks(+) }		↦c { this.keyframes = $sym5 }

	<> keyframes_names → τid │ string

	<> keyframes_blocks → keyframe_selector(+,) { declaration_list ;? }			↦c { this.selectors = $sym1; this.props = $$sym2.props; }

	<> keyframe_selector → τfrom │ τto │ percentage

# adapted from https://developer.mozilla.org/en-US/docs/Web/CSS/@supports#Formal_syntax

	<> supports → \@ τsupports supports_condition { GROUP_RULE_BODY }

	<> supports_condition → τnot supports_in_parens
		│ supports_in_parens ( ( τand │ τor )  supports_in_parens )(*)

	<> supports_in_parens → \( supports_condition \)
		│ supports_feature
		│ general_enclosed

	<> supports_feature → supports_feature_fn 
		│ supports_decl 

	<> supports_decl → \( declaration \)
	<> supports_feature_fn →  θselector \( COMPLEX_SELECTOR \)

# adapted from https://developer.mozilla.org/en-US/docs/Web/CSS/@media#Formal_syntax

	<> media →  \@ (ERR g:ws) τmedia media_query(+,) { GROUP_RULE_BODY } 		↦c ^media

	<> media_query → media_condition 											
		│ (τnot │ τonly )? media_type (τand media_condition_without_or ↦r{ {type:$sym1, val:$sym2 }})?		
																				↦r { {condition:$sym1 || "", type:"query", val:[$sym2, $sym3] } }
	

	<> media_condition → media_condition_without_or
		│ media_or

	<> media_condition_without_or → media_not
		│ media_and 
		│ media_in_parenths

	<> media_not → τnot media_in_parenths										↦r { {type:"not", val:$sym2 } }
	<> media_and → media_in_parenths (τand media_in_parenths)(+)
	<> media_or → media_in_parenths (τor media_in_parenths)(+)

	<> media_in_parenths → \( media_condition \) 								↦r { {type:"parenthesis", val:$sym2 } }				
		│ media_feature
		│ general_enclosed

	<> media_feature → \( ( mf_plain │ mf_boolean │ mf_range ) \) 				↦r { {type:"feature", val:$sym2 } }	
	<> general_enclosed → identifier\( (θany│θws)(*") \)
		│ identifier (θany│θws)(*") 										↦r { {type:"general", name:$sym1, val:$sym3 } }	

	<> mf_plain → mf_name  \: mf_value 											↦r { {type:"keyvalue", key:$sym1, val:$sym3 } }	

	<> mf_range → mf_name ( (< │ <=) │ (> │ >=) │ = ) mf_value
		│ mf_value ( (< │ <=) │ (> │ >=) │ = ) mf_name
		│ mf_value ( > │ >= ) identifier ( > │ >= ) mf_value
		│ mf_value ( < │ <= ) identifier ( < │ <= ) mf_value

	<> mf_value → θnum │ dimension │ mf_name │ ratio
	<> mf_boolean → τtrue | τfalse
	<> mf_name → identifier
	<> media_type → identifier													↦r { {type:"type", val:$sym1 } }

	<> ratio → θnum / θnum 														↦r { {type:"ratio", num:$sym1, den:$sym3 } }
	<> percentage → θnum %  													↦r { new env.fn.percentage($sym1+$sym2) }
	<> dimension → θnum θid  													↦r { new env.fn.length($sym1+$sym2) }

	<> url →  τurl \( string \) 												

	<>string → " θany(+") " │ ' θany(+") '

##############################################################################################
#### Modified from https://drafts.csswg.org/selectors-4/#grammar
##############################################################################################

	<> COMPLEX_SELECTOR →  COMPOUND_SELECTOR ( COMBINATOR? COMPOUND_SELECTOR ↦c ^comboSelector)(*)  ↦c ^selector
	

	<> COMPOUND_SELECTOR → (RED g:ws)  TYPE_SELECTOR? SUBCLASS_SELECTOR(*) ( PSEUDO_ELEMENT_SELECTOR PSEUDO_CLASS_SELECTOR(*) )(*) 
		↦c ^compoundSelector

	<> COMBINATOR →  >  				
		│ + 	
		│ ~ 								
		│ \|| 								

	<> TYPE_SELECTOR →  WQ_NAME 													↦c ^typeselector
		│ NS_PREFIX? \*																↦r {new fn.type_selector([$$sym1, $$sym2])}
	
	<> NS_PREFIX →  (identifier │ \* )? \| 

	<> WQ_NAME →  NS_PREFIX? identifier 											↦r {[$sym1, $$sym2]}

	<> SUBCLASS_SELECTOR →  ID_SELECTOR 
		│ CLASS_SELECTOR 
		│ ATTRIBUTE_SELECTOR 
		│ PSEUDO_CLASS_SELECTOR

	<> ID_SELECTOR →  \# (ERR g:ws) identifier  									↦c ^idSelector

	<> CLASS_SELECTOR →  \. (ERR g:ws) identifier 									↦c ^classSelector

	<> ATTRIBUTE_SELECTOR →  \[ WQ_NAME \] 											↦c ^attribSelector
		│ \[ WQ_NAME ATTR_MATCHER ( identifier │ θstr ) ATTR_MODIFIER? \]			↦c ^attribSelector

	<> ATTR_MATCHER → ~=												
		│ ^= 																
		│ $= 																
		│ *=																
		│ = 																

	<> ATTR_MODIFIER →  τi 
		│ τs

	<> PSEUDO_CLASS_SELECTOR →  \: (ERR g:ws) identifier ( \( COMPLEX_SELECTOR \) )?   		↦c ^pseudoClassSelector

	<> PSEUDO_ELEMENT_SELECTOR →  \: (ERR g:ws) PSEUDO_CLASS_SELECTOR 							↦c ^pseudoElementSelector

##############################################################################################
#### Body
##############################################################################################

	<> declaration_list → (declaration)(+\;) \;?									↦r { $$sym1 }
		│ AT_RULE 																	↦r { $$sym1 }
		│ declaration_list AT_RULE 													↦r { $$sym1.push($$sym2), $$sym1 }
		│ declaration_list (declaration)(+;) ;?	 									↦r { $$sym1.push(...$$sym2), $$sym1 }
		

	<> declaration → identifier \: declaration_values ( \! τimportant )?			↦r ^parseDeclaration

	<> declaration_values → ( θws │ θany │ θid)(+")
		│\( declaration_values \)	 												↦r { $$sym1 + $$sym2 + $$sym3 }
		│ declaration_values declaration_values 									↦r { $$sym1 + $$sym2 }

	<> identifier → (RED θws)  css_id_symbols  				 

	<> css_id_symbols →  css_id_symbols θid          ↦r { $sym1 + $sym2 }
	    │ css_id_symbols θkeyword                    ↦r { $sym1 + $sym2 }
	    │ css_id_symbols \_                          ↦r { $sym1 + $sym2 }
	    │ css_id_symbols \-                          ↦r { $sym1 + $sym2 }
	    │ css_id_symbols \$                          ↦r { $sym1 + $sym2 }
	    │ css_id_symbols θnum                        ↦r { $sym1 + $sym2 }
	    │ css_id_symbols θhex                        ↦r { $sym1 + $sym2 }
	    │ css_id_symbols θbin                        ↦r { $sym1 + $sym2 }
	    │ css_id_symbols θoct                        ↦r { $sym1 + $sym2 }
	    │ \_ 
	    │ \$ 
	    │ θid

